# 批量修改文件名方法

批量修改文件名dos脚本

这个dos脚本方便小白使用非常简单，用户群体为 windows 用户

功能：

1、批量修改文件名

2、批量删除修改文件明中的指定汉字，字母，数字等字符

**脚本源码如下：**
	
	chcp 65001
	@echo off & setlocal enabledelayedexpansion
	
	echo 此批处理可批量替换本文件所在文件夹下所有文件(文件夹)名。
	echo.
	set /p str1= 请输入要替换的文件(文件夹)名字符串（可替换空格）：
	set /p str2= 请输入替换后的文件(文件夹)名字符串（去除则直接回车）：
	echo.
	echo 正在替换文件名……
	for /f "delims=" %%a in ('dir /a-d /s /b') do (
	if "%%~nxa" neq "%~nx0" (
	set "f=%%~na"
	set "f=!f:%str1%=%str2%!"
	if not exist "%%~dpa!f!%%~xa" ren "%%a" "!f!%%~xa"
	)
	)
	echo 替换文件名完成
	echo.
	echo 正在替换文件夹名……
	:folder
	set n=0
	for /f "delims=" %%i in ('dir /ad /s /b ^|find "%str1%"') do (
	set t=%%~ni
	set t=!t:%str1%=%str2%!
	if not exist "%%~dpi!t!" ren "%%i" "!t!" 2>nul
	set /a n+=1
	)
	if "!n!" neq "0" goto folder
	echo 替换文件夹名完成
	pause


脚本使用方法：

第一步：新建一个文件（文件名可自定义）将脚本复制到文件中保存

第二步：修改文件后缀名为.bat 格式，修改好后就可以打开文件了。

第三步：出现黑框，提示 “请输入要替换的文件(文件夹)名字符串（可替换空格）”，此时输入原始文件名然后回车一下

第四步：出现黑框，提示 “请输入替换后的文件(文件夹)名字符串（去除则直接回车）” 此时输入你要替换文字回车一下，最后就ok

 
**可能遇到的问题**

修改文件后缀名方法如下：

这里以 win10 为例子，快捷键 win + e ，点击左上角的查看，把文件扩展名选中。就修改成功了

建议：把你要替换的文件和脚本文件放入同一个目录


简易操作方法：直接下载脚本

[**脚本下载地址**](../file/fileNameReplaScrip.bat)